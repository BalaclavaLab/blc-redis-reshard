apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'com.bmuschko.docker-java-application'

group 'com.balaclavalab'
version '0.3.2'

description = 'BLC Redis reshard utility'
mainClassName = 'com.balaclavalab.redis.ReshardCli'

sourceCompatibility = 15
targetCompatibility = 15

repositories {
    mavenLocal()
    mavenCentral()
}

buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        gradlePluginPortal()
    }
    dependencies {
        classpath 'com.bmuschko:gradle-docker-plugin:6.6.1'
    }
}

dependencies {
    implementation 'io.lettuce:lettuce-core:6.1.0.RELEASE'
    implementation 'commons-cli:commons-cli:1.4'
    testCompile 'org.testcontainers:testcontainers:1.15.2'
}

docker {
    javaApplication {
        baseImage = 'openjdk:15-slim'
        ports = [8080]
        images = ["balaclavalab/${applicationName}:${version}"]
    }
}

task dockerBuildxImage(type:Exec) {
    group 'docker'
    dependsOn dockerCreateDockerfile
    workingDir 'build/docker'
    executable 'docker'
    args = ['buildx', 'build', '--platform', 'linux/amd64,linux/arm64', '-t', "balaclavalab/$project.name:${project.version}", '-t', "balaclavalab/$project.name:latest", '--push', '.']
}
